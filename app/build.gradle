apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId 'com.ztz.modmenu'
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 10
        versionName '0.1.0'
        
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        externalNativeBuild {
            ndkBuild {
                arguments getProcessesCountArg()
            }
        }
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/jniLibs'
        }
    }

    ndkVersion '21.4.7075529'
}

//dependencies must be placed below 'android' brackets to get it work on AIDE
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

def getProcessesCountArg() {
    def procCount = Runtime.runtime.availableProcessors()

    Gradle gradle = getGradle()
    def isClean = gradle.getStartParameter().getTaskNames().find { it ==~ /.*clean.*/ }
    if (isClean) {
        procCount = 1
    }

    return "-j" + procCount
}
